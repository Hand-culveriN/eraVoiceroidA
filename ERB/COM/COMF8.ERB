;-------------------------------------------------
;胸愛撫
;愛撫系コマンド、レベル2
;-------------------------------------------------
@COM8
PRINTL 胸愛撫
STR:0 = 胸愛撫
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 5
LOSEBASE:気力 += 50

SOURCE:情愛 = 100
SOURCE:性行動 = 50
SOURCE:達成感 = 60
SOURCE:屈従 = 10
SOURCE:反感追加 = 20
SOURCE:18 = 20


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:恭順追加 += 20

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF TEQUIP:25
	;奴隷のＢに触手の汚れがつく
	STAIN:胸 |= 64
ELSE
	;奴隷の胸の汚れがなければ口をつかう（愛液と母乳よごれはＯＫ、汚れ無視か助手調教時は問題なし）
	IF STAIN:胸 < 2 || STAIN:胸 == 16 || STAIN:胸 == 17 || ASSIPLAY == 1 || TALENT:MASTER:汚れ無視 == 1
		;調教者のTALENT:舌使いをみる
		IF TALENT:PLAYER:舌使い
			TIMES SOURCE:快Ｂ , 1.40
			SOURCE:恭順追加 += SOURCE:快Ｂ/20
		ENDIF
		;奴隷のＢ⇔調教者の口の汚れが移動
		STAIN:胸 |= STAIN:PLAYER:口
		STAIN:PLAYER:口 |= STAIN:胸
	ENDIF
	;奴隷のＢ⇔調教者の指の汚れが移動
	STAIN:胸 |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:胸
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
A = 5
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;依存度ベクトル
TFLAG:33 = 1


RETURN 1

