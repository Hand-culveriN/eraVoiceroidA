;-------------------------------------------------
;助手を犯す
;特殊コマンド
;-------------------------------------------------
@COM185
PRINTL 助手を犯す
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;共通処理
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0
	


;-------------------------------------------------
;実行決定
;-------------------------------------------------
STR:0 = 助手を犯す
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1500

;ABL:調教者の技巧をみる
IF ABL:PLAYER:技巧 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:技巧 == 1
	TIMES B , 1.20
ELSEIF ABL:PLAYER:技巧 == 2
	TIMES B , 1.40
ELSEIF ABL:PLAYER:技巧 == 3
	TIMES B , 1.60
ELSEIF ABL:PLAYER:技巧 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.00
ENDIF

;ABL:助手の従順をみる
IF ABL:ASSI:従順 == 0
	TIMES B , 0.80
ELSEIF ABL:ASSI:従順 == 1
	TIMES B , 0.90
ELSEIF ABL:ASSI:従順 == 2
	TIMES B , 1.00
ELSEIF ABL:ASSI:従順 == 3
	TIMES B , 1.10
ELSEIF ABL:ASSI:従順 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:助手の欲望をみる
IF ABL:ASSI:欲望 == 0
	TIMES B , 1.00
ELSEIF ABL:ASSI:欲望 == 1
	TIMES B , 1.10
ELSEIF ABL:ASSI:欲望 == 2
	TIMES B , 1.20
ELSEIF ABL:ASSI:欲望 == 3
	TIMES B , 1.30
ELSEIF ABL:ASSI:欲望 == 4
	TIMES B , 1.40
ELSE
	TIMES B , 1.50
ENDIF

;助手のEXP:Ｖ経験をみる
IF EXP:ASSI:Ｖ経験 < EXPLV:1
	TIMES B , 0.20
ELSEIF EXP:ASSI:Ｖ経験 < EXPLV:2
	TIMES B , 0.50
ELSEIF EXP:ASSI:Ｖ経験 < EXPLV:3
	TIMES B , 0.80
ELSEIF EXP:ASSI:Ｖ経験 < EXPLV:4
	TIMES B , 1.00
ELSEIF EXP:ASSI:Ｖ経験 < EXPLV:5
	TIMES B , 1.20
ELSE
	TIMES B , 1.40
ENDIF

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:Ｃ感覚 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:Ｃ感覚 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:Ｃ感覚 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:24 > 0
	B /= 20


SIF TALENT:PLAYER:半人半妖 || TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	BASE:PLAYER:射精 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 0
LOSEBASE:気力 += 250

SOURCE:性行動 = 200
SOURCE:液体追加 = 100
SOURCE:逸脱 = 1500
SOURCE:快Ｂ = 700
SOURCE:18 = 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:恭順追加 += 700

;助手が処女喪失をした場合、屈服と逸脱を増加
IF TALENT:ASSI:処女 == 1 || TALENT:ASSI:再生処女 == 1
	TIMES SOURCE:屈従 , 20.00
	TIMES SOURCE:逸脱 , 3.00
ENDIF

;助手と奴隷の相性によって、全体のソースが変化
T = NO:TARGET
A = NO:ASSI
SOURCE:性行動 *= RELATION:A
SOURCE:液体追加 *= RELATION:A
SOURCE:逸脱 *= RELATION:A
SOURCE:恭順追加 *= RELATION:A
SOURCE:快Ｂ *= RELATION:A
SOURCE:18 *= RELATION:A
SOURCE:性行動 /= 100
SOURCE:液体追加 /= 100
SOURCE:逸脱 /= 100
SOURCE:恭順追加 /= 100
SOURCE:快Ｂ /= 100
SOURCE:18 /= 100

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 6
	;射精先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
IF TALENT:PLAYER:半人半妖 || TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	;主人のＰ⇔助手のＶの汚れが移動
	STAIN:MASTER:ペニス |= STAIN:ASSI:ヴァギナ
	STAIN:ASSI:ヴァギナ |= STAIN:MASTER:ペニス
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;助手が処女の場合、助手と奴隷に異常経験付加
T = NO:TARGET
A = NO:ASSI
IF TALENT:ASSI:処女 == 1
	TALENT:ASSI:処女 = 0
	PRINTL ＜助手が処女＞
	IF RELATION:A > 100
		EXP:異常経験 += 2
		PRINTFORML %EXPNAME:50%＋２
	ELSE
		EXP:異常経験 += 1
		PRINTFORML %EXPNAME:50%＋１
	ENDIF
	EXP:ASSI:異常経験 += 1
	;助手が再生処女の場合、奴隷にのみ異常経験付加
ELSEIF TALENT:ASSI:再生処女 == 1
	TALENT:ASSI:再生処女 = 0
	PRINTL ＜助手が処女＞
	IF RELATION:A > 100
		EXP:異常経験 += 2
		PRINTFORML %EXPNAME:50%＋２
	ELSE
		EXP:異常経験 += 1
		PRINTFORML %EXPNAME:50%＋１
	ENDIF
ENDIF

;助手のＶ経験
EXP:ASSI:Ｖ経験 += 1

;助手のレズ経験
IF TALENT:MASTER:オトコ == 0 && TALENT:ASSI:オトコ == 0
	PRINTS EXPNAME:40
	PRINT +10
	PRINTFORML (%CALLNAME:ASSI%) 
	EXP:ASSI:レズ経験 += 10
	A = NO:PLAYER + 899
	CFLAG:ASSI:A += 10
ENDIF

;依存度ベクトル
TFLAG:33 = 5

T = NO:TARGET
A = NO:ASSI
;依存度補正値(助手との相性100↑かつ淫乱で1、娼婦で2）
IF TALENT:淫乱 && RELATION:A > 100
	TFLAG:30 += 1
ELSEIF TALENT:娼婦 && RELATION:A > 100
	TFLAG:30 += 2
ENDIF

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_185
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 2
	PRINTS ABLNAME:1
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 2,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 2
	PRINTS MARKNAME:1
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 2,')
	S = 1
ENDIF

;PALAM:欲情
IF PALAM:欲情 < PALAMLV:1
	L = 0
ELSEIF PALAM:欲情 < PALAMLV:2
	L = 1
ELSEIF PALAM:欲情 < PALAMLV:3
	L = 2
ELSEIF PALAM:欲情 < PALAMLV:4
	L = 3
ELSEIF PALAM:欲情 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 2
	PRINTS PALAMNAME:6
	PRINTV 'LV,L,'(,L * 2,')
	S = 1
ENDIF

;助手のABL:露出癖
IF ABL:ASSI:露出癖
	SIF S
		PRINT  + 
	A += ABL:ASSI:露出癖 * 2
	PRINT 助手の露出癖
	PRINTV 'LV,ABL:ASSI:露出癖,'(,ABL:ASSI:露出癖 * 2,')
	S = 1
ENDIF

;助手が恋慕
IF TALENT:ASSI:恋慕 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 10
	PRINT 助手が恋慕
	PRINTV '(,10,')
	S = 1
ENDIF

;助手が親愛
IF TALENT:ASSI:親愛 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 25
	PRINT 助手が親愛
	PRINTV '(,25,')
	S = 1
ENDIF

;助手が処女
IF TALENT:ASSI:処女 == 1
	PRINT  - 
	A -= 50
	PRINT 助手が処女
	PRINTV '(,50,')
	S = 1
ENDIF

;合計を表示(40以上で実行)
PRINT  = 
PRINTV A

V = 40
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

RETURN 1

