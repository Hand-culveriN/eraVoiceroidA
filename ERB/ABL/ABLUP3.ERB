;Ｃ感覚の可否
@ISABLUP3
CALL DECIDE_ABLUP3
RETURN RESULT

;Ｃ感覚
@ABLUP3
DRAWLINE
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:Ｃ感覚 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:Ｃ感覚 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP3

PRINT [0] - 
PRINTS PALAMNAME:0
PRINT の珠×
PRINTV CJuelRequire
PRINT ……
IF CCond == 0
	PRINT ＯＫ
ELSE
	SIF CCond & 1
		PRINT 珠不足 
	SIF CCond & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF CCond != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:Ｃ感覚 += 1

IF RESULT == 0
	JUEL:快Ｃ -= CJuelRequire
ENDIF

PRINTS ABLNAME:3
PRINT のレベルが
PRINTV ABL:Ｃ感覚
PRINTW になりました


;C感覚LvUP判定
@DECIDE_ABLUP3
#DIM DYNAMIC ABLUPAble = 0
ABLUPAble = 0
;条件別にＯＫかダメかを記録する
VARSET CCond, 0


;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:Ｃ感覚 >= 10
	CCond = 256
	RETURN 0
ELSEIF ABL:Ｃ感覚 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	CCond = 256
	RETURN 0
ENDIF


IF ABL:Ｃ感覚 == 0
	CJuelRequire = 1
ELSEIF ABL:Ｃ感覚 == 1
	CJuelRequire = 20
ELSEIF ABL:Ｃ感覚 == 2
	CJuelRequire = 400
ELSEIF ABL:Ｃ感覚 == 3
	CJuelRequire = 5000
ELSEIF ABL:Ｃ感覚 == 4
	CJuelRequire = 15000
ELSEIF ABL:Ｃ感覚 == 5
	CJuelRequire = 30000
ELSEIF ABL:Ｃ感覚 == 6
	CJuelRequire = 40000
ELSEIF ABL:Ｃ感覚 == 7
	CJuelRequire = 50000
ELSEIF ABL:Ｃ感覚 == 8
	CJuelRequire = 60000
ELSEIF ABL:Ｃ感覚 == 9
	CJuelRequire = 70000
ENDIF

;淫乱
SIF TALENT:淫乱
	TIMES CJuelRequire , 0.80
;娼婦
SIF TALENT:娼婦
	TIMES CJuelRequire , 0.50

;0になった場合は1にする
SIF CJuelRequire == 0
	CJuelRequire = 1

;快Ｃの珠で上げる
SIF JUEL:快Ｃ < CJuelRequire
	CCond |= 1

IF CCond == 0
	ABLUPAble = 1
ENDIF
RETURN ABLUPAble

;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP3
#DIM DYNAMIC LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP3
SIF RESULT == 0
	RETURN 0

;習得の珠
FOR COUNT, 0, 9999 - ABL:Ｃ感覚
	CALL DECIDE_ABLUP3
	IF CCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:Ｃ感覚 += 1
	JUEL:快Ｃ -= CJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(3, 0, -1, -1, LvUPNum)
LvUPNum = 0
