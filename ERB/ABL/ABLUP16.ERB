;精液中毒の可否
@ISABLUP16
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:精液中毒 >= 10
	RETURN 0
ELSEIF ABL:精液中毒 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP16

RETURN RESULT


;精液中毒
@ABLUP16
DRAWLINE
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:精液中毒 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:精液中毒 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP16

;ＬＶ２から３に上げるときは異常経験必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
IF ABL:精液中毒 == 2 && TALENT:汚臭鈍感 == 0 && TALENT:中毒しやすい == 0
	PRINTS EXPNAME:50
	PRINTL あり、かつ
;ＬＶ３から４に上げるときは異常経験2以上必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
ELSEIF ABL:精液中毒 == 3 && TALENT:汚臭鈍感 == 0 && TALENT:中毒しやすい == 0
	PRINTS EXPNAME:50
	PRINTL 2以上、かつ
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
ELSEIF ABL:精液中毒 == 4 && TALENT:汚臭鈍感 == 0 && TALENT:中毒しやすい == 0
	PRINTS EXPNAME:50
	PRINTL 3以上、かつ
;ＬＶ８から９に上げるときは異常経験7以上必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
ELSEIF ABL:精液中毒 == 8 && TALENT:汚臭鈍感 == 0 && TALENT:中毒しやすい == 0
	PRINTS EXPNAME:50
	PRINTL 7以上、かつ
ENDIF

;奉仕精神が必要
PRINTS ABLNAME:7
PRINTV ABL:精液中毒 + 1
PRINTL LV以上、かつ


PRINT [0] - 
PRINTS PALAMNAME:6
PRINT の珠×
PRINTV YokujouJuelRequire
PRINT 、
PRINTS PALAMNAME:7
PRINT の珠×
PRINTV KuppukuJuelRequire
PRINT 、
PRINTS EXPNAME:20
PRINTV ExpRequire
PRINT 以上
PRINT ……

IF FirstCond == 0
	PRINT ＯＫ
ELSE
	SIF FirstCond & 1
		PRINT 珠不足 
	SIF FirstCond & 2
		PRINT 経験不足 
	SIF FirstCond & 4
		PRINT 能力不足 
ENDIF
PRINTL 


PRINT [1] - 
PRINTS PALAMNAME:6
PRINT の珠×
PRINTV YokujouJuelRequire*3
PRINT 、
PRINTS PALAMNAME:7
PRINT の珠×
PRINTV KuppukuJuelRequire*3
PRINT 、
PRINTS EXPNAME:20
PRINTV ExpRequire/2
PRINT 以上
PRINT ……

IF SecondCond == 0
	PRINT ＯＫ
ELSE
	SIF SecondCond & 1
		PRINT 珠不足 
	SIF SecondCond & 2
		PRINT 経験不足 
	SIF SecondCond & 4
		PRINT 能力不足 
ENDIF
PRINTL 


PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF FirstCond != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF SecondCond != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:精液中毒 += 1

IF RESULT == 0
	JUEL:欲情 -= YokujouJuelRequire
	JUEL:屈服 -= KuppukuJuelRequire
ELSEIF RESULT == 1
	JUEL:欲情 -= YokujouJuelRequire*3
	JUEL:屈服 -= KuppukuJuelRequire*3
ENDIF

PRINTS ABLNAME:16
PRINT のレベルが
PRINTV ABL:精液中毒
PRINTW になりました

;精液中毒LVUP判定
@DECIDE_ABLUP16
#DIM ABLUPAble = 0
;条件別にＯＫかダメかを記録する
FirstCond = 0
SecondCond = 0


IF ABL:精液中毒 == 0
	YokujouJuelRequire = 3000
	KuppukuJuelRequire = 10000
	ExpRequire = 10
ELSEIF ABL:精液中毒 == 1
	YokujouJuelRequire = 8000
	KuppukuJuelRequire = 25000
	ExpRequire = 25
ELSEIF ABL:精液中毒 == 2
	YokujouJuelRequire = 15000
	KuppukuJuelRequire = 50000
	ExpRequire = 40
ELSEIF ABL:精液中毒 == 3
	YokujouJuelRequire = 30000
	KuppukuJuelRequire = 100000
	ExpRequire = 80
ELSEIF ABL:精液中毒 == 4
	YokujouJuelRequire = 55000
	KuppukuJuelRequire = 300000
	ExpRequire = 200
ELSEIF ABL:精液中毒 == 5
	YokujouJuelRequire = 60000
	KuppukuJuelRequire = 350000
	ExpRequire = 250
ELSEIF ABL:精液中毒 == 6
	YokujouJuelRequire = 65000
	KuppukuJuelRequire = 400000
	ExpRequire = 300
ELSEIF ABL:精液中毒 == 7
	YokujouJuelRequire = 70000
	KuppukuJuelRequire = 450000
	ExpRequire = 350
ELSEIF ABL:精液中毒 == 8
	YokujouJuelRequire = 75000
	KuppukuJuelRequire = 500000
	ExpRequire = 400
ELSEIF ABL:精液中毒 == 9
	YokujouJuelRequire = 90000
	KuppukuJuelRequire = 650000
	ExpRequire = 500
ENDIF

;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES YokujouJuelRequire , 2.00
	TIMES KuppukuJuelRequire , 2.00
	TIMES ExpRequire , 2.00
ENDIF
;中毒しやすい
IF TALENT:中毒しやすい
	TIMES YokujouJuelRequire , 0.50
	TIMES KuppukuJuelRequire , 0.50
	TIMES ExpRequire , 0.50
	TIMES D , 0.50
ENDIF

;ＬＶ２から３に上げるときは異常経験必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
IF ABL:精液中毒 == 2 && TALENT:汚臭鈍感 == 0 && TALENT:中毒しやすい == 0
	IF EXP:異常経験 == 0
		;異常経験が不足
		FirstCond |= 2
		SecondCond |= 2
	ENDIF
;ＬＶ３から４に上げるときは異常経験2以上必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
ELSEIF ABL:精液中毒 == 3 && TALENT:汚臭鈍感 == 0 && TALENT:中毒しやすい == 0
	IF EXP:異常経験 < 2
		;異常経験が不足
		FirstCond |= 2
		SecondCond |= 2
	ENDIF
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
ELSEIF ABL:精液中毒 == 4 && TALENT:汚臭鈍感 == 0 && TALENT:中毒しやすい == 0
	IF EXP:異常経験 < 3
		;異常経験が不足
		FirstCond |= 2
		SecondCond |= 2
	ENDIF
;ＬＶ８から９に上げるときは異常経験7以上必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
ELSEIF ABL:精液中毒 == 8 && TALENT:汚臭鈍感 == 0 && TALENT:中毒しやすい == 0
	IF EXP:異常経験 < 7
		;異常経験が不足
		FirstCond |= 2
		SecondCond |= 2
	ENDIF
ENDIF

;奉仕精神が必要
IF ABL:奉仕精神 < ABL:精液中毒 + 1
	FirstCond |= 4
	SecondCond |= 4
ENDIF

;欲情の珠で上げる
SIF JUEL:欲情 < YokujouJuelRequire
	FirstCond |= 1
;屈服の珠で上げる
SIF JUEL:屈服 < KuppukuJuelRequire
	FirstCond |= 1
;精液経験が必要
SIF EXP:精液経験 < ExpRequire
	FirstCond |= 2

IF FirstCond == 0
	ABLUPAble = 1
ENDIF


;欲情の珠で上げる
SIF JUEL:欲情 < YokujouJuelRequire*3
	SecondCond |= 1
;屈服の珠で上げる
SIF JUEL:屈服 < KuppukuJuelRequire*3
	SecondCond |= 1
;精液経験が必要
SIF EXP:精液経験 < ExpRequire/2
	SecondCond |= 2

IF SecondCond == 0
	ABLUPAble = 1
ENDIF
RETURN ABLUPAble

;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP16
#DIM LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP16
SIF RESULT == 0
	RETURN 0

FOR COUNT, 0, 9999 - ABL:精液中毒
	CALL DECIDE_ABLUP16
	IF FirstCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:精液中毒 += 1
	JUEL:欲情 -= YokujouJuelRequire
	JUEL:屈服 -= KuppukuJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(16, 6, 7, -1, LvUPNum)
LvUPNum = 0

FOR COUNT, 0, 9999 - ABL:精液中毒
	CALL DECIDE_ABLUP16
	IF SecondCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:精液中毒 += 1
	JUEL:欲情 -= YokujouJuelRequire * 3
	JUEL:屈服 -= KuppukuJuelRequire * 3
NEXT
CALL PRINT_AUT_ABLUP(16, 6, 7, -1, LvUPNum)
LvUPNum = 0