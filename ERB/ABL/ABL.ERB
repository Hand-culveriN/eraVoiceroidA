;BEGIN ABLUPで最初に呼び出される関数。
@SHOW_JUEL
#DIM LineNum = 0
#DIM PalamNo = 0
DRAWLINE
T = TARGET
CALL SHOW_INFO_EXP


REPEAT 13
	IF COUNT == 12
		PalamNo = 100
	ELSE
		PalamNo = COUNT
	ENDIF

	PRINTFORM %PALAMNAME:PalamNo%の珠：

	;表示位置そろえる
	IF JUEL:PalamNo < 10
		PRINT       
	ELSEIF JUEL:PalamNo < 100
		PRINT      
	ELSEIF JUEL:PalamNo < 1000
		PRINT     
	ELSEIF JUEL:PalamNo < 10000
		PRINT    
	ELSEIF JUEL:PalamNo < 100000
		PRINT   
	ELSEIF JUEL:PalamNo < 1000000
		PRINT  
	ENDIF
	PRINTV JUEL:PalamNo

	;改行or表示位置あけ
	IF LineNum % 4 == 3
		PRINTL 
	ELSE
		PRINT 　
	ENDIF
	LineNum = LineNum + 1
REND
PRINTL 　
DRAWLINE




@SHOW_ABLUP_SELECT
#DIM ABLNo = 0
FOR ABLNo, 0, 21

	TRYCALLFORM ISABLUP{ABLNo}
	;以下は昔の悪しきコード　こういうコードは書かないように！
	;IF COUNT == 0
	;	X = 0
	;	CALL ISABLUP0
	;ELSEIF COUNT == 1
	;	X = 1
	;	CALL ISABLUP1
	;略
	;ELSEIF COUNT == 21
	;	X = 21
	;	CALL ISABLUP21
	;ENDIF
	
	SIF (ABLNo == 11 || ABLNo == 17) && TALENT:オトコ
		CONTINUE
	SIF (ABLNo == 20 || ABLNo == 21) && TALENT:オトコ == 0
		CONTINUE
	PRINTFORM [{ABLNo}]%ABLNAME:ABLNo%-LV{ABL:ABLNo}

	;レベルアップ可能ならばアスタリスクをつける
	SIF RESULT == 1
		PRINT  *
	PRINTL 
NEXT

CALL ISABLUP99
PRINTFORM [99]%MARKNAME:3%-LV{MARK:反発刻印}
SIF RESULT == 1
	PRINT  *
PRINTL 

PRINTL [100] 一括レベルアップ
PRINTL [999] 能力値アップの終了

@USERABLUP
IF RESULT == 100
	CALL AUT_LVUPPER, 0
ELSEIF RESULT == 999
	CALL JUJUN_UP_CHECK
	CALL YOKUBO_UP_CHECK
	BEGIN TURNEND
	RETURN 1
ENDIF

RETURN 0
