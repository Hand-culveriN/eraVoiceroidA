;露出癖の可否
@ISABLUP8
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:露出癖 >= 10
	RETURN 0
ELSEIF ABL:露出癖 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP8

RETURN RESULT


;露出癖
@ABLUP8
DRAWLINE
IF ABL:露出癖 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:露出癖 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP8

;ＬＶ３から４に上げるときは異常経験必要（素質：[目立ちたがり]なら無視できる）
IF ABL:露出癖 == 3 && TALENT:目立ちたがり == 0
	PRINTS EXPNAME:50
	PRINTL あり、かつ
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[目立ちたがり]なら無視できる）
ELSEIF ABL:露出癖 == 4 && TALENT:目立ちたがり == 0
	PRINTS EXPNAME:50
	PRINTL 3以上、かつ
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[目立ちたがり]なら無視できる）
ELSEIF ABL:露出癖 == 8 && TALENT:目立ちたがり == 0
	PRINTS EXPNAME:50
	PRINTL 5以上、かつ
ENDIF

;欲望が必要
PRINTS ABLNAME:1
PRINTV ABL:露出癖 + 1
PRINTL LV以上、かつ

PRINT [0] - 
PRINTS PALAMNAME:9
PRINT の珠×
PRINTV ChijouJuelRequire
IF ABL:露出癖 < 2
	PRINT 、
	PRINTS EXPNAME:2
	PRINTV 1
	PRINT 以上
ELSE
	PRINT 、
	PRINTS EXPNAME:11
	PRINTV 1
	PRINT 以上
ENDIF
PRINT ……

IF ChijouCond == 0
	PRINT ＯＫ
ELSE
	SIF ChijouCond & 1
		PRINT 珠不足 
	SIF ChijouCond & 2
		PRINT 経験不足 
	SIF ChijouCond & 4
		PRINT 能力不足 
ENDIF
PRINTL 


PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF ChijouCond != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:露出癖 += 1

IF RESULT == 0
	JUEL:恥情 -= ChijouJuelRequire
ENDIF

PRINTS ABLNAME:8
PRINT のレベルが
PRINTV ABL:露出癖
PRINTW になりました

;露出癖LVUP判定
@DECIDE_ABLUP8
#DIM DYNAMIC ABLUPAble = 0
ABLUPAble = 0
;条件別にＯＫかダメかを記録する
ChijouCond = 0
J = 0
K = 0
;能力アップ可能なら1にする

IF ABL:露出癖 == 0
	ChijouJuelRequire = 100
ELSEIF ABL:露出癖 == 1
	ChijouJuelRequire = 1000
ELSEIF ABL:露出癖 == 2
	ChijouJuelRequire = 5000
ELSEIF ABL:露出癖 == 3
	ChijouJuelRequire = 15000
ELSEIF ABL:露出癖 == 4
	ChijouJuelRequire = 35000
ELSEIF ABL:露出癖 == 5
	ChijouJuelRequire = 35000
ELSEIF ABL:露出癖 == 6
	ChijouJuelRequire = 35000
ELSEIF ABL:露出癖 == 7
	ChijouJuelRequire = 35000
ELSEIF ABL:露出癖 == 8
	ChijouJuelRequire = 35000
ELSEIF ABL:露出癖 == 9
	ChijouJuelRequire = 35000
ENDIF

;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES ChijouJuelRequire , 2.00
ENDIF

;欲望が露出癖＋１レベルでないといけない
IF ABL:欲望 < ABL:露出癖+1
	;欲望が不足
	ChijouCond |= 4
ENDIF

;ＬＶ３から４に上げるときは異常経験必要（素質：[目立ちたがり]なら無視できる）
IF ABL:露出癖 == 3 && TALENT:目立ちたがり == 0
	IF EXP:異常経験 == 0
		;異常経験が不足
		ChijouCond |= 2
	ENDIF
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[目立ちたがり]なら無視できる）
ELSEIF ABL:露出癖 == 4 && TALENT:目立ちたがり == 0
	IF EXP:異常経験 < 3
		;異常経験が不足
		ChijouCond |= 2
	ENDIF
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[目立ちたがり]なら無視できる）
ELSEIF ABL:露出癖 == 8 && TALENT:目立ちたがり == 0
	IF EXP:異常経験 < 5
		;異常経験が不足
		ChijouCond |= 2
	ENDIF
ENDIF

;恥情の珠で上げる
SIF JUEL:恥情 < ChijouJuelRequire
	ChijouCond |= 1
IF ABL:露出癖 < 2
	;絶頂経験が必要
	SIF EXP:絶頂経験 == 0
		ChijouCond |= 2
ELSE
	;調教自慰経験が必要
	SIF EXP:調教自慰経験 == 0
		ChijouCond |= 2
ENDIF

IF ChijouCond == 0
	ABLUPAble = 1
ENDIF

RETURN ABLUPAble

;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP8
#DIM DYNAMIC LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP8
SIF RESULT == 0
	RETURN 0

;恥情の珠
FOR COUNT, 0, 9999 - ABL:露出癖
	CALL DECIDE_ABLUP8
	IF ChijouCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:露出癖 += 1
	JUEL:恥情 -=  ChijouJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(8, 9, -1, -1, LvUPNum)
LvUPNum = 0