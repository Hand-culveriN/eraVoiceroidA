;歌唱技能アップ可能なら1を、不可能なら0を返す
@ISABLUP14
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:歌唱技能 >= 10
	RETURN 0
ELSEIF ABL:歌唱技能 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP14

RETURN RESULT


;歌唱技能
@ABLUP14
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:歌唱技能 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:歌唱技能 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP14

PRINT [0] - 
PRINTS PALAMNAME:8
PRINT の珠×
PRINTV ShutokuJuelRequire
PRINT 、
PRINTS EXPNAME:64
PRINTV ExpRequire
PRINT 以上
PRINT ……
IF ShutokuCond == 0
	PRINT ＯＫ
ELSE
	SIF ShutokuCond & 1
		PRINT 珠不足 
	SIF ShutokuCond & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF ShutokuCond != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:歌唱技能 += 1

IF RESULT == 0
	JUEL:習得 -= ShutokuJuelRequire
ENDIF

PRINTS ABLNAME:14
PRINT のレベルが
PRINTV ABL:歌唱技能
PRINTW になりました


;歌唱技能LVUP判定
@DECIDE_ABLUP14
#DIM ABLUPAble = 0
;条件別にＯＫかダメかを記録する
ShutokuCond = 0



IF ABL:歌唱技能 == 0
	ShutokuJuelRequire = 75
	ExpRequire = 5
ELSEIF ABL:歌唱技能 == 1
	ShutokuJuelRequire = 500
	ExpRequire = 10
ELSEIF ABL:歌唱技能 == 2
	ShutokuJuelRequire = 3000
	ExpRequire = 30
ELSEIF ABL:歌唱技能 == 3
	ShutokuJuelRequire = 9000
	ExpRequire = 50
ELSEIF ABL:歌唱技能 == 4
	ShutokuJuelRequire = 24000
	ExpRequire = 100
ELSEIF ABL:歌唱技能 == 5
	ShutokuJuelRequire = 30000
	ExpRequire = 200
ELSEIF ABL:歌唱技能 == 6
	ShutokuJuelRequire = 35000
	ExpRequire = 300
ELSEIF ABL:歌唱技能 == 7
	ShutokuJuelRequire = 40000
	ExpRequire = 400
ELSEIF ABL:歌唱技能 == 8
	ShutokuJuelRequire = 45000
	ExpRequire = 500
ELSEIF ABL:歌唱技能 == 9
	ShutokuJuelRequire = 50000
	ExpRequire = 600
ENDIF

;恋慕
IF TALENT:恋慕
	TIMES ShutokuJuelRequire , 0.60
	TIMES ExpRequire , 0.60
ENDIF
;親愛
IF TALENT:親愛
	TIMES ShutokuJuelRequire , 0.40
	TIMES ExpRequire , 0.40
ENDIF
;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES ShutokuJuelRequire , 2.00
	TIMES ExpRequire , 2.00
ENDIF


;習得の珠で上げる
SIF JUEL:習得 < ShutokuJuelRequire
	ShutokuCond |= 1
SIF EXP:歌唱経験 < ExpRequire
	ShutokuCond |= 2
IF ShutokuCond == 0
	ABLUPAble = 1
ENDIF

RETURN ABLUPAble

;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP14
#DIM LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP14
SIF RESULT == 0
	RETURN 0

;習得の珠
FOR COUNT, 0, 9999 - ABL:歌唱技能
	CALL DECIDE_ABLUP14
	IF ShutokuCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:歌唱技能 += 1
	JUEL:習得 -=  ShutokuJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(14, 8, -1, -1, LvUPNum)
LvUPNum = 0
