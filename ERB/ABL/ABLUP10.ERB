;サドっ気の可否
@ISABLUP10
CALL DECIDE_ABLUP10

RETURN RESULT


;サドっ気
@ABLUP10
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:サドっ気 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:サドっ気 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP10

;欲望がサドっ気＋１レベルでないといけない
PRINTS ABLNAME:1
PRINTV ABL:サドっ気 + 1
PRINTL LV以上、かつ

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[サド]なら無視できる）
IF ABL:サドっ気 == 3 && TALENT:サド == 0
	PRINTS EXPNAME:50
	PRINTL あり、かつ
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[サド]なら無視できる）
ELSEIF ABL:サドっ気 == 4 && TALENT:サド == 0
	PRINTS EXPNAME:50
	PRINTL 3以上、かつ
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[サド]なら無視できる）
ELSEIF ABL:サドっ気 == 8 && TALENT:サド == 0
	PRINTS EXPNAME:50
	PRINTL 5以上、かつ
ENDIF


PRINT [0] - 
PRINTS PALAMNAME:6
PRINT の珠×
PRINTV YokujouJuelRequire
PRINT 、
PRINTS EXPNAME:32
PRINTV ExpRequire
PRINT 以上
PRINT 、
PRINTS EXPNAME:2
PRINTV 1
PRINT 以上
PRINT ……

IF YokujouCond == 0
	PRINT ＯＫ
ELSE
	SIF YokujouCond & 1
		PRINT 珠不足 
	SIF YokujouCond & 2
		PRINT 経験不足 
	SIF YokujouCond & 4
		PRINT 能力不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF RESULT != 0 && RESULT != 100
	RESTART
ELSEIF YokujouCond != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:サドっ気 += 1

IF RESULT == 0
	JUEL:欲情 -= YokujouJuelRequire
ENDIF

PRINTS ABLNAME:10
PRINT のレベルが
PRINTV ABL:サドっ気
PRINTW になりました

;サドっ気LVUP判定
@DECIDE_ABLUP10
#DIM DYNAMIC ABLUPAble = 0
ABLUPAble = 0
;条件別にＯＫかダメかを記録する
YokujouCond = 0
J = 0


;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:サドっ気 >= 10
	YokujouCond = 256
	RETURN 0
ELSEIF ABL:サドっ気 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	YokujouCond = 256
	RETURN 0
ENDIF


IF ABL:サドっ気 == 0
	YokujouJuelRequire = 100
	ExpRequire = 5
ELSEIF ABL:サドっ気 == 1
	YokujouJuelRequire = 500
	ExpRequire = 10
ELSEIF ABL:サドっ気 == 2
	YokujouJuelRequire = 1500
	ExpRequire = 20
ELSEIF ABL:サドっ気 == 3
	YokujouJuelRequire = 3000
	ExpRequire = 50
ELSEIF ABL:サドっ気 == 4
	YokujouJuelRequire = 5000
	ExpRequire = 80
ELSEIF ABL:サドっ気 == 5
	YokujouJuelRequire = 8000
	ExpRequire = 100
ELSEIF ABL:サドっ気 == 6
	YokujouJuelRequire = 12000
	ExpRequire = 150
ELSEIF ABL:サドっ気 == 7
	YokujouJuelRequire = 15000
	ExpRequire = 200
ELSEIF ABL:サドっ気 == 8
	YokujouJuelRequire = 25000
	ExpRequire = 250
ELSEIF ABL:サドっ気 == 9
	YokujouJuelRequire = 30000
	ExpRequire = 350
ENDIF

;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES YokujouJuelRequire , 2.00
	TIMES ExpRequire , 2.00
ENDIF
;サド
IF TALENT:サド
	TIMES YokujouJuelRequire , 0.50
	TIMES ExpRequire , 0.50
ENDIF

;欲望がサドっ気＋１レベルでないといけない
IF ABL:欲望 < ABL:サドっ気+1
	;欲望が不足
	YokujouCond |= 4
ENDIF

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[サド]なら無視できる）
IF ABL:サドっ気 == 3 && TALENT:サド == 0
	IF EXP:異常経験 == 0
		;異常経験が不足
		YokujouCond |= 2
	ENDIF
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[サド]なら無視できる）
ELSEIF ABL:サドっ気 == 4 && TALENT:サド == 0
	IF EXP:異常経験 < 3
		;異常経験が不足
		YokujouCond |= 2
	ENDIF
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[サド]なら無視できる）
ELSEIF ABL:サドっ気 == 8 && TALENT:サド == 0
	IF EXP:異常経験 < 5
		;異常経験が不足
		YokujouCond |= 2
	ENDIF
ENDIF

;欲情の珠で上げる
SIF JUEL:欲情 < YokujouJuelRequire
	YokujouCond |= 1
;嗜虐快楽経験が必要な場合
SIF EXP:嗜虐快楽経験 < ExpRequire
	YokujouCond |= 2

SIF YokujouCond == 0
	ABLUPAble = 1

RETURN ABLUPAble

;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP10
#DIM DYNAMIC LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP10
SIF RESULT == 0
	RETURN 0

;欲情の珠
FOR COUNT, 0, 9999 - ABL:サドっ気
	CALL DECIDE_ABLUP10
	IF YokujouCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:サドっ気 += 1
	JUEL:欲情 -=  YokujouJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(10, 6, -1, -1, LvUPNum)
LvUPNum = 0