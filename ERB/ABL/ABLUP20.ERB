;ホモっ気の可否
@ISABLUP20
CALL DECIDE_ABLUP20

RETURN RESULT


;ホモっ気
@ABLUP20
DRAWLINE
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:ホモっ気 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:ホモっ気 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP20

;ＬＶ３から４に上げるときは異常経験必要（素質：[両刀]なら無視できる）
IF ABL:ホモっ気 == 3 && TALENT:両刀 == 0
	PRINTS EXPNAME:50
	PRINTL あり、かつ
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[両刀]なら無視できる）
ELSEIF ABL:ホモっ気 == 4 && TALENT:両刀 == 0
	PRINTS EXPNAME:50
	PRINTL 3以上、かつ
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[両刀]なら無視できる）
ELSEIF ABL:ホモっ気 == 8 && TALENT:両刀 == 0
	PRINTS EXPNAME:50
	PRINTL 5以上、かつ
ENDIF

PRINT [0] - 
PRINTS PALAMNAME:6
PRINT の珠×
PRINTV YokujouJuelRequire
IF KuppukuJuelRequire > 0
	PRINT 、
	PRINTS PALAMNAME:7
	PRINT の珠×
	PRINTV KuppukuJuelRequire
ENDIF
PRINT 、
PRINTS EXPNAME:41
PRINTV ExpRequire
PRINT 以上
PRINT ……

IF FirstCond == 0
	PRINT ＯＫ
ELSE
	SIF FirstCond & 1
		PRINT 珠不足 
	SIF FirstCond & 2
		PRINT 経験不足 
	SIF FirstCond & 4
		PRINT 能力不足 
ENDIF
PRINTL 

IF CJuelRequire > 0
	PRINT [1] - 
	PRINTS PALAMNAME:0
	PRINT の珠×
	PRINTV CJuelRequire
	PRINT 、
	PRINTS EXPNAME:41
	PRINTV ExpRequire
	PRINT 以上
	PRINT ……
	
	IF SecondCond == 0
		PRINT ＯＫ
	ELSE
		SIF SecondCond & 1
			PRINT 珠不足 
		SIF SecondCond & 2
			PRINT 経験不足 
		SIF SecondCond & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ELSE
	SecondCond = 256
ENDIF

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF FirstCond != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF SecondCond == 256 && RESULT == 1
	RESTART
ELSEIF SecondCond != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:ホモっ気 += 1

IF RESULT == 0
	JUEL:欲情 -= YokujouJuelRequire
	JUEL:屈服 -= KuppukuJuelRequire
ELSEIF RESULT == 1
	JUEL:快Ｃ -= CJuelRequire
ENDIF

PRINTS ABLNAME:20
PRINT のレベルが
PRINTV ABL:ホモっ気
PRINTW になりました


;ホモっ気のLVUP判定
@DECIDE_ABLUP20
#DIM DYNAMIC ABLUPAble = 0
ABLUPAble = 0
;条件別にＯＫかダメかを記録する
FirstCond = 0
SecondCond = 0

;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:ホモっ気 >= 10
	FirstCond = 256
	SecondCond = 256
	RETURN 0
ELSEIF ABL:ホモっ気 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	FirstCond = 256
	SecondCond = 256
	RETURN 0
ENDIF



IF ABL:ホモっ気 == 0
	YokujouJuelRequire = 200
	ExpRequire = 50
	KuppukuJuelRequire = 0
	CJuelRequire = 1000
ELSEIF ABL:ホモっ気 == 1
	YokujouJuelRequire = 1000
	ExpRequire = 200
	KuppukuJuelRequire = 0
	CJuelRequire = 5000
ELSEIF ABL:ホモっ気 == 2
	YokujouJuelRequire = 3000
	ExpRequire = 500
	KuppukuJuelRequire = 1000
	CJuelRequire = 0
ELSEIF ABL:ホモっ気 == 3
	YokujouJuelRequire = 8000
	ExpRequire = 1000
	KuppukuJuelRequire = 2000
	CJuelRequire = 0
ELSEIF ABL:ホモっ気 == 4
	YokujouJuelRequire = 20000
	ExpRequire = 2000
	KuppukuJuelRequire = 5000
	CJuelRequire = 0
ELSEIF ABL:ホモっ気 == 5
	YokujouJuelRequire = 25000
	ExpRequire = 2300
	KuppukuJuelRequire = 6000
	CJuelRequire = 0
ELSEIF ABL:ホモっ気 == 6
	YokujouJuelRequire = 30000
	ExpRequire = 2600
	KuppukuJuelRequire = 8000
	CJuelRequire = 0
ELSEIF ABL:ホモっ気 == 7
	YokujouJuelRequire = 35000
	ExpRequire = 3000
	KuppukuJuelRequire = 10000
	CJuelRequire = 0
ELSEIF ABL:ホモっ気 == 8
	YokujouJuelRequire = 40000
	ExpRequire = 3500
	KuppukuJuelRequire = 15000
	CJuelRequire = 0
ELSEIF ABL:ホモっ気 == 9
	YokujouJuelRequire = 50000
	ExpRequire = 4000
	KuppukuJuelRequire = 20000
	CJuelRequire = 0
ENDIF

;淫乱
SIF TALENT:淫乱
	TIMES YokujouJuelRequire , 0.80
;娼婦
SIF TALENT:娼婦
	TIMES YokujouJuelRequire , 0.50
;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES YokujouJuelRequire , 2.00
	TIMES ExpRequire , 2.00
	TIMES KuppukuJuelRequire , 2.00
	TIMES CJuelRequire , 2.00
ENDIF

;ＬＶ３から４に上げるときは異常経験必要（素質：[両刀]なら無視できる）
IF ABL:ホモっ気 == 3 && TALENT:両刀 == 0
	IF EXP:異常経験 == 0
		;異常経験が不足
		FirstCond |= 2
		SecondCond |= 2
	ENDIF
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[両刀]なら無視できる）
ELSEIF ABL:ホモっ気 == 4 && TALENT:両刀 == 0
	IF EXP:異常経験 < 3
		;異常経験が不足
		FirstCond |= 2
		SecondCond |= 2
	ENDIF
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[両刀]なら無視できる）
ELSEIF ABL:ホモっ気 == 8 && TALENT:両刀 == 0
	IF EXP:異常経験 < 5
		;異常経験が不足
		FirstCond |= 2
		SecondCond |= 2
	ENDIF
ENDIF

;欲情の珠で上げる
SIF JUEL:欲情 < YokujouJuelRequire
	FirstCond |= 1
;屈服の珠で上げる
SIF JUEL:屈服 < KuppukuJuelRequire
	FirstCond |= 1
;ゲイ経験が必要
SIF EXP:ゲイ経験 < ExpRequire
	FirstCond |= 2

IF FirstCond == 0
	ABLUPAble = 1
ENDIF

IF CJuelRequire > 0
	;快Ｃの珠で上げる
	SIF JUEL:快Ｃ < CJuelRequire
		SecondCond |= 1
	;ゲイ経験が必要
	SIF EXP:ゲイ経験 < ExpRequire
		SecondCond |= 2
	
	IF SecondCond == 0
		ABLUPAble = 1
	ENDIF
ELSE
	SecondCond = 256
ENDIF
RETURN ABLUPAble

;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP20
#DIM DYNAMIC LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP20
SIF RESULT == 0
	RETURN 0

;第二条件
FOR COUNT, 0, 9999 - ABL:ホモっ気
	CALL DECIDE_ABLUP20
	IF SecondCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:ホモっ気 += 1
	JUEL:快Ｃ -= CJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(20, 0, -1, -1, LvUPNum)
LvUPNum = 0

;第一条件
FOR COUNT, 0, 9999 - ABL:ホモっ気
	CALL DECIDE_ABLUP20
	IF FirstCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:ホモっ気 += 1
	JUEL:欲情 -= YokujouJuelRequire
	JUEL:屈服 -= KuppukuJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(20, 6, 7, -1, LvUPNum)
LvUPNum = 0

