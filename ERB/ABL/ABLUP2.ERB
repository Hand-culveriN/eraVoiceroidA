;技巧の可否
@ISABLUP2
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:技巧 >= 10
	RETURN 0
ELSEIF ABL:技巧 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP2
RETURN RESULT


;技巧
@ABLUP2
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:技巧 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:技巧 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP2

PRINT [0] - 
PRINTS PALAMNAME:8
PRINT の珠×
PRINTV ShutokuJuelRequire
PRINT ……
IF ShutokuCond == 0
	PRINT ＯＫ
ELSE
	SIF ShutokuCond & 1
		PRINT 珠不足 
	SIF ShutokuCond & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF ShutokuCond != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:技巧 += 1

IF RESULT == 0
	JUEL:習得 -= ShutokuJuelRequire
ENDIF

PRINTS ABLNAME:2
PRINT のレベルが
PRINTV ABL:技巧
PRINTW になりました

;技巧LVUP判定
@DECIDE_ABLUP2
#DIM ABLUPAble = 0
;条件別にＯＫかダメかを記録する
ShutokuCond = 0
;能力アップ可能なら1にする
ABLUPAble = 0

IF ABL:技巧 == 0
	ShutokuJuelRequire = 1
ELSEIF ABL:技巧 == 1
	ShutokuJuelRequire = 20
ELSEIF ABL:技巧 == 2
	ShutokuJuelRequire = 200
ELSEIF ABL:技巧 == 3
	ShutokuJuelRequire = 3000
ELSEIF ABL:技巧 == 4
	ShutokuJuelRequire = 20000
ELSEIF ABL:技巧 == 5
	ShutokuJuelRequire = 25000
ELSEIF ABL:技巧 == 6
	ShutokuJuelRequire = 34000
ELSEIF ABL:技巧 == 7
	ShutokuJuelRequire = 45000
ELSEIF ABL:技巧 == 8
	ShutokuJuelRequire = 55000
ELSEIF ABL:技巧 == 9
	ShutokuJuelRequire = 70000
ENDIF

;自尊心
IF TALENT:プライド高い
	TIMES ShutokuJuelRequire , 1.50
ENDIF
;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES ShutokuJuelRequire , 2.00
ENDIF

;0になった場合は1にする
SIF ShutokuJuelRequire == 0
	ShutokuJuelRequire = 1

;習得の珠で上げる
SIF JUEL:習得 < ShutokuJuelRequire
	ShutokuCond |= 1

IF ShutokuCond == 0
	ABLUPAble = 1
ENDIF
RETURN ABLUPAble

;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP2
#DIM LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP2
SIF RESULT == 0
	RETURN 0

;習得の珠
FOR COUNT, 0, 9999 - ABL:技巧
	CALL DECIDE_ABLUP2
	IF ShutokuCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:技巧 += 1
	JUEL:習得 -= ShutokuJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(2, 8, -1, -1, LvUPNum)
LvUPNum = 0

