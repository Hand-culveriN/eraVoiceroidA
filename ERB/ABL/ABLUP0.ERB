;従順の可否
;返り値　1：可能　2：不可
@ISABLUP0
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:従順 >= 10
	RETURN 0
ELSEIF ABL:従順 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	RETURN 0
ENDIF

RETURN DECIDE_ABLUP0()



;従順
@ABLUP0
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:従順 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:従順 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

;このLOCALに意味はない
LOCAL:99 = DECIDE_ABLUP0()


;ＬＶ４から５に上げるときは異常経験必要（素質：[臆病][素直][恋慕][親愛]なら無視できる）
IF ABL:従順 == 4 && TALENT:臆病 == 0 && TALENT:素直 == 0 && TALENT:恋慕 == 0 && TALENT:親愛 == 0
	PRINTS EXPNAME:50
	PRINTL あり、かつ
;ＬＶ７から８に上げるときは異常経験3以上必要（素質：[臆病][素直][恋慕][親愛]なら無視できる）
ELSEIF ABL:従順 == 7 && TALENT:臆病 == 0 && TALENT:素直 == 0 && TALENT:恋慕 == 0 && TALENT:親愛 == 0
	PRINTS EXPNAME:50
	PRINTL 3以上、かつ
ENDIF

PRINTFORM [0] - %PALAMNAME:11%の珠×{KyoufuJuelRequire}……
IF KyoufuCond:1 == 0
	PRINT ＯＫ
ELSE
	SIF KyoufuCond:1 & 1
		PRINT 珠不足 
	SIF KyoufuCond:1 & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTFORM [1] - %PALAMNAME:5%の珠×{KyoujunJuelRequire}……

IF KyoujunCond:1 == 0
	PRINT ＯＫ
ELSE
	SIF KyoujunCond:1 & 1
		PRINT 珠不足 
	SIF KyoujunCond:1 & 2
		PRINT 経験不足 
ENDIF
PRINTL 

IF YokujouJuelRequire > 0
	;欲情の珠で上げる
	SIF JUEL:欲情 < YokujouJuelRequire
		YokujouCond:1 |= 1

	PRINTFORM [2] - %PALAMNAME:6%の珠×{YokujouJuelRequire}……

	IF YokujouCond:1 == 0
		PRINT ＯＫ
	ELSE
		SIF YokujouCond:1 & 1
			PRINT 珠不足 
		SIF YokujouCond:1 & 2
			PRINT 経験不足 
	ENDIF
	PRINTL 
ELSE
	YokujouCond:1 = 256
ENDIF

IF KuppukuJuelRequire > 0
	;屈服の珠で上げる
	SIF JUEL:屈服 < KuppukuJuelRequire
		KuppukuCond:1 |= 1

	PRINTFORM [3] - %PALAMNAME:7%の珠×{KuppukuJuelRequire}……

	IF KuppukuCond:1 == 0
		PRINT ＯＫ
	ELSE
		SIF KuppukuCond:1 & 1
			PRINT 珠不足 
		SIF KuppukuCond:1 & 2
			PRINT 経験不足 
	ENDIF
	PRINTL 
ELSE
	KuppukuCond:1 = 256
ENDIF

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 3) && RESULT != 100
	RESTART
ELSEIF KyoufuCond:1 != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF KyoujunCond:1 != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	RESTART
ELSEIF YokujouCond:1 == 256 && RESULT == 2
	RESTART
ELSEIF YokujouCond:1 != 0 && RESULT == 2
	PRINTL 条件を満たしていません
	RESTART
ELSEIF KuppukuCond:1 == 256 && RESULT == 3
	RESTART
ELSEIF KuppukuCond:1 != 0 && RESULT == 3
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:従順 += 1

IF RESULT == 0
	JUEL:恐怖 -= KyoufuJuelRequire
ELSEIF RESULT == 1
	JUEL:恭順 -= KyoujunJuelRequire
ELSEIF RESULT == 2
	JUEL:欲情 -= YokujouJuelRequire
ELSEIF RESULT == 3
	JUEL:屈服 -= KuppukuJuelRequire
ENDIF

PRINTS ABLNAME:0
PRINT のレベルが
PRINTV ABL:従順
PRINTW になりました


;従順LvUP判定
;返り値　1：能力アップ可能　0：不可能
@DECIDE_ABLUP0
#FUNCTION
#DIM ABLUPAble = 0



;条件別にＯＫかダメかを記録する
ExpCond = 1
VARSET KyoufuCond, 0
VARSET KyoujunCond, 0
VARSET YokujouCond, 0
VARSET KuppukuCond, 0
KyoufuCond:1 = 0
KyoujunCond:1 = 0
YokujouCond:1 = 0
KuppukuCond:1 = 0
;能力アップ可能なら1にする
;Z = 0


;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:従順 >= 10
	RETURNF 0
ELSEIF ABL:従順 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	RETURNF 0
ENDIF

IF ABL:従順 == 0
	KyoufuJuelRequire = 10
	KyoujunJuelRequire = 10
	YokujouJuelRequire = 150
	KuppukuJuelRequire = 100
ELSEIF ABL:従順 == 1
	KyoufuJuelRequire = 150
	KyoujunJuelRequire = 100
	YokujouJuelRequire = 450
	KuppukuJuelRequire = 600
ELSEIF ABL:従順 == 2
	KyoufuJuelRequire = 500
	KyoujunJuelRequire = 300
	YokujouJuelRequire = 1000
	KuppukuJuelRequire = 1200
ELSEIF ABL:従順 == 3
	KyoufuJuelRequire = 3000
	KyoujunJuelRequire = 3000
	YokujouJuelRequire = 0
	KuppukuJuelRequire = 0
ELSEIF ABL:従順 == 4
	KyoufuJuelRequire = 6000
	KyoujunJuelRequire = 6000
	YokujouJuelRequire = 0
	KuppukuJuelRequire = 0
ELSEIF ABL:従順 == 5
	KyoufuJuelRequire = 8000
	KyoujunJuelRequire = 10000
	YokujouJuelRequire = 0
	KuppukuJuelRequire = 0
ELSEIF ABL:従順 == 6
	KyoufuJuelRequire = 10000
	KyoujunJuelRequire = 15000
	YokujouJuelRequire = 0
	KuppukuJuelRequire = 0
ELSEIF ABL:従順 == 7
	KyoufuJuelRequire = 14000
	KyoujunJuelRequire = 20000
	YokujouJuelRequire = 0
	KuppukuJuelRequire = 0
ELSEIF ABL:従順 == 8
	KyoufuJuelRequire = 18000
	KyoujunJuelRequire = 25000
	YokujouJuelRequire = 0
	KuppukuJuelRequire = 0
ELSEIF ABL:従順 == 9
	KyoufuJuelRequire = 25000
	KyoujunJuelRequire = 35000
	YokujouJuelRequire = 0
	KuppukuJuelRequire = 0
ENDIF


;恋慕
SIF TALENT:恋慕
	TIMES KyoujunJuelRequire , 0.80
;親愛
SIF TALENT:親愛
	TIMES KyoujunJuelRequire , 0.60
;服従
SIF TALENT:服従
	TIMES KyoufuJuelRequire , 0.70
;隷属
SIF TALENT:隷属
	TIMES KyoufuJuelRequire , 0.40

;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES KyoufuJuelRequire , 2.00
	TIMES KyoujunJuelRequire , 2.00
	TIMES YokujouJuelRequire , 2.00
	TIMES KuppukuJuelRequire , 2.00
ENDIF

;ＬＶ４から５に上げるときは異常経験必要（素質：[臆病][素直][恋慕][親愛]なら無視できる）
IF ABL:従順 == 4 && TALENT:臆病 == 0 && TALENT:素直 == 0 && TALENT:恋慕 == 0 && TALENT:親愛 == 0
	IF EXP:異常経験 == 0
		;異常経験がない
		KyoufuCond:1 |= 2
		KyoujunCond:1 |= 2
		YokujouCond:1 |= 2
		KuppukuCond:1 |= 2
		ExpCond = 0
	ENDIF
;ＬＶ７から８に上げるときは異常経験3以上必要（素質：[臆病][素直][恋慕][親愛]なら無視できる）
ELSEIF ABL:従順 == 7 && TALENT:臆病 == 0 && TALENT:素直 == 0 && TALENT:恋慕 == 0 && TALENT:親愛 == 0
	IF EXP:異常経験 < 3
		;異常経験が3未満
		KyoufuCond:1 |= 2
		KyoujunCond:1 |= 2
		YokujouCond:1 |= 2
		KuppukuCond:1 |= 2
		ExpCond = 0
	ENDIF
ENDIF

;恐怖の珠で上げる
SIF JUEL:恐怖 < KyoufuJuelRequire
	KyoufuCond:1 |= 1

IF KyoufuCond:1 == 0
	ABLUPAble = 1
ENDIF

;恭順の珠で上げる
SIF JUEL:恭順 < KyoujunJuelRequire
	KyoujunCond:1 |= 1

IF KyoujunCond:1 == 0
	ABLUPAble = 1
ENDIF

IF YokujouJuelRequire
	;欲情の珠で上げる
	SIF JUEL:欲情 < YokujouJuelRequire
		YokujouCond:1 |= 1

	IF YokujouCond:1 == 0
		ABLUPAble = 1
	ENDIF
ELSE
	YokujouCond:1 = 256
ENDIF

IF KuppukuJuelRequire
	;屈服の珠で上げる
	SIF JUEL:屈服 < KuppukuJuelRequire
		KuppukuCond:1 |= 1

	IF KuppukuCond:1 == 0
		ABLUPAble = 1
	ENDIF
ELSE
	KuppukuCond:1 = 256
ENDIF

RETURNF ABLUPAble

;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP0
#DIM LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP0
SIF RESULT == 0
	RETURN 0

;恐怖の珠
FOR COUNT, 0, 9999 - ABL:従順
	LOCAL:99 = DECIDE_ABLUP0()
	IF KyoufuCond:1 != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:従順 += 1
	JUEL:恐怖 -= KyoufuJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(0, 11, -1, -1, LvUPNum)
LvUPNum = 0



;恭順の珠
FOR COUNT, 0, 9999 - ABL:従順
	LOCAL:99 = DECIDE_ABLUP0()
	IF KyoujunCond:1 != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:従順 += 1
	JUEL:恭順 -= KyoujunJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(0, 5, -1, -1, LvUPNum)
LvUPNum = 0

;欲情の珠
FOR COUNT, 0, 9999 - ABL:従順
	LOCAL:99 = DECIDE_ABLUP0()
	IF YokujouCond:1 != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:従順 += 1
	JUEL:欲情 -= YokujouJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(0, 6, -1, -1, LvUPNum)
LvUPNum = 0

;屈服の珠
FOR COUNT, 0, 9999 - ABL:従順
	LOCAL:99 = DECIDE_ABLUP0()
	IF KuppukuCond:1 != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:従順 += 1
	JUEL:屈服 -= KuppukuJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(0, 7, -1, -1, LvUPNum)
LvUPNum = 0
