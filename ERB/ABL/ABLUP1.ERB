;欲望の可否
@ISABLUP1
CALL DECIDE_ABLUP1
RETURN RESULT

;欲望
@ABLUP1
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:欲望 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:欲望 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP1

;ＬＶ４から５に上げるときは異常経験必要（素質：[快感に素直][淫乱][娼婦]なら無視できる）
IF ABL:欲望 == 4 && TALENT:快感に素直 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTS EXPNAME:50
	PRINTL あり、かつ
;ＬＶ７から８に上げるときは異常経験3以上必要（素質：[快感に素直][淫乱][娼婦]なら無視できる）
ELSEIF ABL:欲望 == 7 && TALENT:快感に素直 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTS EXPNAME:50
	PRINTL 3以上、かつ
ENDIF

PRINT [0] - 
PRINTS PALAMNAME:6
PRINT の珠×
PRINTV YokujouJuelRequire
PRINT ……
IF YokujouCond == 0
	PRINT ＯＫ
ELSE
	SIF YokujouCond & 1
		PRINT 珠不足 
	SIF YokujouCond & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF YokujouCond != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:欲望 += 1

IF RESULT == 0
	JUEL:欲情 -= YokujouJuelRequire
ENDIF

PRINTS ABLNAME:1
PRINT のレベルが
PRINTV ABL:欲望
PRINTW になりました

CALL YOKUBO_UP_CHECK

;欲望LvUP判定
@DECIDE_ABLUP1
#DIM DYNAMIC ABLUPAble = 0
ABLUPAble = 0
;ABL系グローバル変数の初期化
CALL ABL_GLOBAL_DISPOSE
;条件別にＯＫかダメかを記録する
YokujouCond = 0

;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:欲望 >= 10
	YokujouCond = 256
	RETURN 0
ELSEIF ABL:欲望 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	YokujouCond = 256
	RETURN 0
ENDIF


IF ABL:欲望 == 0
	YokujouJuelRequire = 1
ELSEIF ABL:欲望 == 1
	YokujouJuelRequire = 20
ELSEIF ABL:欲望 == 2
	YokujouJuelRequire = 1000
ELSEIF ABL:欲望 == 3
	YokujouJuelRequire = 8000
ELSEIF ABL:欲望 == 4
	YokujouJuelRequire = 24000
ELSEIF ABL:欲望 == 5
	YokujouJuelRequire = 30000
ELSEIF ABL:欲望 == 6
	YokujouJuelRequire = 36000
ELSEIF ABL:欲望 == 7
	YokujouJuelRequire = 45000
ELSEIF ABL:欲望 == 8
	YokujouJuelRequire = 55000
ELSEIF ABL:欲望 == 9
	YokujouJuelRequire = 70000
ENDIF

;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES YokujouJuelRequire , 2.00
ENDIF

;ＬＶ４から５に上げるときは異常経験必要（素質：[快感に素直][淫乱][娼婦]なら無視できる）
IF ABL:欲望 == 4 && TALENT:快感に素直 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	IF EXP:異常経験 == 0
		;異常経験がない
		YokujouCond |= 2
	ENDIF
;ＬＶ７から８に上げるときは異常経験3以上必要（素質：[快感に素直][淫乱][娼婦]なら無視できる）
ELSEIF ABL:欲望 == 7 && TALENT:快感に素直 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	IF EXP:異常経験 < 3
		;異常経験が3未満
		YokujouCond |= 2
	ENDIF
ENDIF

;0になった場合は1にする
SIF YokujouJuelRequire == 0
	YokujouJuelRequire = 1

;欲情の珠で上げる
SIF JUEL:欲情 < YokujouJuelRequire
	YokujouCond |= 1

IF YokujouCond == 0
	ABLUPAble = 1
ENDIF
RETURN ABLUPAble



;自動でABLを上げるメソッド（J+に倣って実装）
@AUT_ABLUP1
#DIM DYNAMIC LvUPNum = 0

;上げられなかったら無視
CALL ISABLUP1
SIF RESULT == 0
	RETURN 0

;欲情の珠
FOR COUNT, 0, 9999 - ABL:欲望
	CALL DECIDE_ABLUP1
	IF YokujouCond != 0
		BREAK
	ENDIF
	LvUPNum += 1
	ABL:欲望 += 1
	JUEL:欲情 -= YokujouJuelRequire
NEXT
CALL PRINT_AUT_ABLUP(1, 6, -1, -1, LvUPNum)
LvUPNum = 0
